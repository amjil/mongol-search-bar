(ns virtual-keyboard.example.basic
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter.alpha2 :as f]
   [virtual-keyboard.keyboard :as keyboard]
   [virtual-keyboard.keyboard-action :as keyboard-action]
   [virtual-keyboard.options :as options]
   [menu-bar.menu :as menu]))

(defn basic-body []
  (f/widget
   :context ctx
   :get [:text-controller]
   :let [width  (-> m/MediaQuery (.of ctx) .-size .-width)
         height (-> m/MediaQuery (.of ctx) .-size .-height)
         info (merge options/keyboard-option {} {:width width})
         state (atom (merge options/keyboard-state
                            {:text-controller text-controller}))]
   :bind {:info info :state state :context ctx}
   :watch [{:keys [candidates-list]} state]
   (m/SafeArea)
   .child
   (m/Center)
   .child
   (m/Stack)
   .children
   [(m/Column
     .children
     [(m/Expanded
       .child
       (mgl/MongolTextField
        .controller text-controller
        .maxLines 500
        .autofocus true
        .onTap (fn []
                 (swap! state assoc :candidates-list [])
                 (dart:core/print "onTap TextField"))
        .showCursor true
        .readOnly true
        .style (m/TextStyle .fontSize 20)
        .decoration (m/InputDecoration .border (m/OutlineInputBorder))))
      (m/Container
       .color (-> m/Colors .grey .-shade300)
       .child
       (keyboard/keyboard))])
    (m/Positioned
     .bottom (+ options/keyboard-default-height (* 4 (+ 4 (:row-vertical-padding info))))
     .right (/ width 4)
     .child
     (menu/menu {:bar {:elavation 10}
                 :item {:on-tap (fn [x] (keyboard-action/on-candidates-clicked x state))}}
                candidates-list))]))

(defn basic []
  (f/widget
   :context ctx
   :managed [^m/TextEditingController text-controller (m/TextEditingController)]
   :bind {:text-controller text-controller}
   (m/Scaffold
    .resizeToAvoidBottomInset true
    ;; .appBar (m/AppBar
    ;;          .title (m/Text "Virtual Keyboard"))
    .body
    (basic-body))))